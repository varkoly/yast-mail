/**
 * File:
 *   include/mail/ui.ycp
 *
 * Package:
 *   Configuration of mail
 *
 * Summary:
 *   User interface functions.
 *
 * Authors:
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 *
 * All user interface functions.
 *
 */

{

textdomain "mail";

import "Wizard";
import "Progress";
import "Mode";
import "Mail";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";
include "wizard/sequencer.ycp";
include "network/dns.ycp";
include "network/ip.ycp";
include "require.ycp";

include "mail/helps.ycp";
include "mail/widgets.ycp";
include "mail/wj.ycp";

/**
 * A replacement for the Next button: "Finish", `next
 */
global term finish_button = `PushButton (`id (`next), `opt (`default), FinishButtonLabel());

/**
 * Declare a name for the current dialog
 * to ease making screenshots.
 * @param s eg. "mail-1-conntype"
 */
global define void SetScreenShotName (string s) ``{
    UI::SetModulename (s);
}

/**
 * Restore the screenshot name
 * (currently sets fixed default name)
 */
global define void RestoreScreenShotName () ``{
    UI::SetModulename ("yast2");
}

/**
 * A Wizard Sequencer helper
 * @return	`next
 */
global define symbol JustNext () ``{
    return `next;
}

global map dialogs = $[
    "read"	: [ ``( ReadDialog () ), true ],
    "confirm"	: [ ``( ConfirmDialog () ), true],
    "write"	: [ ``( WriteDialog () ), true ],

    "mta":		``( MtaSelectionDialog () ),

    "connection_type":	``( ConnectionTypeDialog () ),
    "basic":		``( BasicDialog () ),
    "outgoing-details":	``( OutgoingDetailsDialog () ),
    "downloading":	``( DownloadingDialog () ),
    "aliases":		``( AliasesDialog () ),
    "virtual":		``( VirtualDialog () ),
    "common-next":	[ ``( JustNext () ), true ],
    ];

global map common_sequence = $[
    //"ws_start" : must be defined in an overriding sequence
    "connection_type" :
    $[
	`abort	: `abort,
	`next	: "basic",
	`none	: "common-next",
	],
    "basic" :
    $[
	`abort	: `abort,
	`outgoing_details : "outgoing-details",
	`downloading : "downloading",
	`aliases	: "aliases",
	`virtual	: "virtual",
	`next	: "common-next",
	],
    "outgoing-details" :
    $[
	`abort	: `abort,
	`next	: "basic",
	],
    "downloading" :
    $[
	`abort	: `abort,
	`next	: "basic",
	],
    "aliases" :
    $[
	`abort	: `abort,
	`next	: "basic",
	],
    "virtual" :
    $[
	`abort	: `abort,
	`next	: "basic",
	],
    "common-next" :
    $[
	`next	: `next,
	],
    ];

/**
 * Whole configuration of mail
 * @return `back, `abort or `next
 */
global define symbol MailSequence () ``{
    map sequence = $[
	"ws_start" : "read",
	"read" :
	$[
	    `abort	: `abort,
	    `next	: "connection_type",
	    ],

	// common_sequence here

	// override
	"common-next" :
	$[
	    `next	: "confirm",
	    ],
	"confirm" :
	$[
	    `next	: "write"
	    ],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	    ]
	];

    // Translators: dialog caption
    string caption = _("Mail configuration");
    term contents = `Label (_("Initializing ..."));

    Wizard::CreateDialog ();
    Wizard::SetContentsButtons ( caption,
				contents,
				"",
				BackButtonLabel (),
				NextButtonLabel ());

    // the second map must override the first!
    sequence = union (common_sequence, sequence);
    any ret = WizardSequencer (dialogs, sequence);

    UI::CloseDialog ();
    return ret;
}

/**
 * Whole configuration of mail but without reading and writing.
 * MTA is selected first.
 * For use with autoinstallation.
 * @return `back, `abort or `next
 */
global define symbol MailAutoSequence () ``{
    map sequence = $[
	"ws_start":	"mta",
	"mta":
	$[
	    `abort	: `abort,
	    `next	: "connection_type",
	    ],

	// common_sequence here
	];

    // Translators: dialog caption
    string caption = _("Mail configuration");
    term contents = `Label (_("Initializing ..."));

    Wizard::CreateDialog ();
    Wizard::SetContentsButtons ( caption,
				contents,
				"",
				BackButtonLabel (),
				NextButtonLabel ());

    // the second map must override the first!
    sequence = union (common_sequence, sequence);
    any ret = WizardSequencer (dialogs, sequence);

    UI::CloseDialog ();
    return ret;
}


/**
 * Read settings dialog
 * @return `abort or `next
 */
global define symbol ReadDialog () ``{
    SetScreenShotName ("mail-0-read");
    // Set help text
    Wizard::RestoreHelp (ReadDialogHelp ());

    // A callback function for abort
    block callback = ``{
	return UI::PollInput () == `abort;
    };

    // Read the configuration
    boolean was_ok = Mail::Read ( callback );

    // TODO FIXME possibly handle the abort
    if (was_ok)
    {
	if (! Mail::CreateConfig ())
	{
	    string setting = "MAIL_CREATE_CONFIG";
	    // Translators: continue/cancel dialog
	    // %1 is a sysconfig variable name
	    was_ok = UI::ContinueCancelPopup (sformat (_("The setting %1 is turned off. You have
probably modified the configuration files directly.
If you continue, it will be turned on and
SuSEconfig will overwrite manual changes.
"), setting));
	}
    }
    RestoreScreenShotName ();
    return ( was_ok? `next : `abort );
}

/**
 * Confirmation dialog before saving and installing needed packages
 * @return `back or `next
 */
global define symbol ConfirmDialog () ``{
    string message1 = _("The configuration will be written now.\n");
    string message2 = Mail::ProbePackages ();
    return UI::ContinueCancelPopup (message1 + message2) ? `next : `back;
}

/**
 * Write settings dialog
 * @return `abort or `next
 */
global define symbol WriteDialog () ``{
    // Install packages if needed.
    // Cannot do it in Write, autoinstall does it differently.
    if (size (Mail::install_packages) > 0 || size (Mail::remove_packages) > 0)
    {
	// require.ycp
	DoInstallAndRemove (Mail::install_packages, Mail::remove_packages);
    }

    // Set help text
    Wizard::RestoreHelp (WriteDialogHelp ());

    // A callback function for abort
    block callback = ``{
	return UI::PollInput () == `abort;
    };

    // Read the configuration
    boolean was_ok = Mail::Write ( callback, false );

    // TODO FIXME possibly handle the abort

    return ( was_ok? `next : `abort );
}

/**
 * MTA selection dialog
 * (only for autoinstallation, otherwise probed in Mail::Read)
 * @return `abort or `next
 */
global define symbol MtaSelectionDialog () ``{
    SetScreenShotName ("mail-0-mta");

    symbol mta = Mail::mta;
    // for now. TODO: disable Next if none selected
    if (mta != `sendmail && mta != `postfix)
    {
	mta = `sendmail;
    }

    // Translators: dialog caption
    // Mailer: Sendmail or Postfix
    string caption = _("Mail transfer agent");
    term contents =
	`Frame (
	    // Translators: frame label
	    // Mailer: Sendmail or Postfix
	    _("Mail transfer agent"),
	    `RadioButtonGroup (
		`id (`mtag),
		`HSquash (
		    `VBox (
			`VSpacing (0.2),
			// MTA name does not need translation
			`Left (`RadioButton (`id (`sendmail), `opt(`autoShortcut), "Sendmail", mta == `sendmail)),
			// MTA name does not need translation
			`Left (`RadioButton (`id (`postfix), `opt(`autoShortcut), "Postfix", mta == `postfix)),
			`VSpacing (0.2)
			)
		    )
		)
	    );

    Wizard::SetContentsButtons (caption, contents, MtaSelectionDialogHelp (), BackButtonLabel (), NextButtonLabel ());

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}

	if (ret == `back || ret == `next ||
	    (ret == `abort && UI::ReallyAbortPopup (Mail::touched)))
	{
	    break;
	}
    }

    if (ret == `next)
    {
	mta = UI::QueryWidget (`id (`mtag), `CurrentButton);
	Mail::Touch (Mail::mta != mta);
	Mail::mta = mta;
    }
    RestoreScreenShotName ();
    return ret;
}

/**
 * D1
 * @return `back, `abort, `next or `none
 */
global define symbol ConnectionTypeDialog () ``{
    SetScreenShotName ("mail-1-conntype");

    symbol ct = Mail::connection_type;

    // Translators: dialog caption
    string caption = _("Connection type");
    term contents =
	`Frame (
	    // Translators: frame label
	    _("Connection type"),
	    `RadioButtonGroup (
		`id (`ctg),
		`HSquash (
		    `VBox (
			`VSpacing (0.2),
			// Translators: radio button label
			`Left (`RadioButton (`id (`permanent), `opt (`notify), _("&Permanent"), ct == `permanent)),
			// Translators: radio button label
			`Left (`RadioButton (`id (`dialup), `opt (`notify), _("&Dial-up"), ct == `dialup)),
			// Translators: radio button label
			`Left (`RadioButton (`id (`none), `opt (`notify), _("No &connection"), ct == `none)),
/*
			`VSpacing (0.5),
			`Left (`RadioButton (`id (`dont), _("Do not configure"), false)),
*/
			`VSpacing (0.2)
			)
		    )
		)
	    );

    Wizard::SetContentsButtons (caption, contents, ConnectionTypeDialogHelp (),
				BackButtonLabel (), NextButtonLabel ());
    any ret = nil;
    while (true)
    {
	ct = UI::QueryWidget (`id (`ctg), `CurrentButton);
	if (ct == `permanent || ct == `dialup)
	{
	    UI::ChangeWidget (`id (`next), `Label, NextButtonLabel());
	    //argh, slow
	    //Wizard::RestoreNextButton ();
	}
	else if (ct == `none)
	{
	    UI::ChangeWidget (`id (`next), `Label, FinishButtonLabel());
	    //argh, slow
	    //Wizard::ReplaceNextButton (finish_button);
	}

	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}

	if (ret == `back || ret == `next ||
	    (ret == `abort && UI::ReallyAbortPopup (Mail::touched)))
	{
	    break;
	}
    }

    if (ret == `next)
    {
	ct = UI::QueryWidget (`id (`ctg), `CurrentButton);
	Mail::Touch (Mail::connection_type != ct);
	Mail::connection_type = ct;

	ret = (ct == `none)? `none: ret;
    }
    RestoreScreenShotName ();
    return ret;
}

/**
 * D2
 * @return `back, `abort, `next or `outgoing_details
 */
global define symbol BasicDialog () ``{
    boolean dialup = (Mail::connection_type == `dialup);
    SetScreenShotName (dialup ? "mail-2b-dialup" : "mail-2a-permanent");

    list(symbol) buttons = [];
    // watch out, fm_widgets are not part of widgets
    // because of special validation requirements
    list(symbol) widgets = [
	`outgoing_mail_server,
	`listen_remote,
	];

    string caption = dialup ?
	// Translators: dialog caption
	_("Dial-up connection settings") :
	// Translators: dialog caption
	_("Permanent connection settings");

    term o_contents =
	`VSquash (
	    `HBox (
		WJ_MakeWidget (`outgoing_mail_server),
		`HSpacing (1),
		// Translators: button
		`Bottom (`PushButton (`id (`outgoing_details), _("&Outgoing details ...")))
		)
	    );
    buttons = add (buttons, `outgoing_details);

    boolean amavis_allowed = Mail::amavis_allowed;
    term i_contents = `VBox ();

    list(symbol) fm_widgets = [];
    if (dialup)
    {
	// Edit the first fetchmail item here.
	// Copy it into the edit buffer.
	fetchmail_item = Mail::fetchmail[0]:$[];

	fm_widgets = [
	    `fm_server,
	    `fm_protocol,
	    `fm_remote_user,
	    `fm_password,
	    `fm_local_user,
	    ];
//	widgets = flatten ([widgets, fm_widgets]);

	term fm_contents = `VBox (
	    `HSpacing (40), // prevent fm_password being squashed in curses
	    `HBox (
		WJ_MakeWidget (`fm_server),
		`HSpacing (1),
		WJ_MakeWidget (`fm_protocol)
		),
	    `HBox (
		WJ_MakeWidget (`fm_remote_user),
		`HSpacing (1),
		WJ_MakeWidget (`fm_password)
		),
	    WJ_MakeWidget (`fm_local_user)
	    );
	term fm_frame = `Frame (_("&Downloading"), fm_contents);
	i_contents = add (i_contents,
			  `HBox (
			      `HSpacing (1),
			      fm_frame,
			      `HSpacing (1)
			      )
	    );
    }
    i_contents = add (i_contents, `Left (WJ_MakeWidget (`listen_remote)));


    if (amavis_allowed)
    {
//	i_contents = add (i_contents, `VSpacing (1));
	// Translators: check box label
	i_contents = add (i_contents, `Left (WJ_MakeWidget (`use_amavis)));
	widgets = add (widgets, `use_amavis);
    }
    else
    {
	contents = add (contents, `Empty (`id (`use_amavis)));
    }

    i_contents = add (i_contents, WJ_MakeWidget (`root_alias));
    widgets = add (widgets, `root_alias);

    i_contents = add (
	i_contents,
	`MenuButton (`id (`incoming_details), _("&Incoming details ..."), [
			 `item (`id (`downloading), _("&Downloading ...")),
			 `item (`id (`aliases), _("&Aliases ...")),
			 `item (`id (`virtual), _("&Virtual domains ...")),
			 ])
	);
    buttons = flatten ([buttons, [`downloading, `aliases, `virtual]]);

    term o_frame = `Frame (_("Outgoing mail"), `HSquash (o_contents));
    term i_frame = `Frame (_("Incoming mail"), `HSquash (i_contents));
    term contents = `VBox (
	`VStretch (),
	o_frame,
	`VStretch (),
//	`VSpacing (0.2),
	i_frame,
	`VStretch ()
	);

    string help = ""; //TODO
    Wizard::SetContentsButtons (
	caption, contents, WJ_MakeHelp (widgets),
	BackButtonLabel (),
	FinishButtonLabel ());

    if (dialup)
    {
	WJ_GetWidget (`fm_protocol);
	WJ_GetWidget (`fm_local_user);
    }

    // nothing entered in the dowloading items - don't save them
    boolean fm_empty = true;

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}

	if (ret == `back ||
	    (ret == `abort && UI::ReallyAbortPopup (Mail::touched)))
	{
	    break;
	}
	else if (ret == `next || contains (buttons, ret))
	{
	    // input validation
	    // For consistency, all querywidgets are done here

	    if (WJ_Validate (widgets))
	    {
		fm_empty =
		    !dialup ||
		    UI::QueryWidget(`id (`fm_server), `Value) == "" &&
		    UI::QueryWidget(`id (`fm_remote_user), `Value) == "" &&
		    UI::QueryWidget(`id (`fm_password), `Value) == "" &&
		    UI::QueryWidget(`id (`fm_local_user), `Value) == "";
		if (fm_empty || WJ_Validate (fm_widgets))
		{
		    // all checks OK, break the input loop
		    break;
		}
	    }
	}
    }

    if (ret == `next || contains (buttons, ret))
    {
	WJ_Set (widgets);
	if (!fm_empty)
	{
	    WJ_Set (fm_widgets);
	    // CHECK: aliasing is not harmful here
	    Mail::fetchmail[0] = fetchmail_item;
	}
    }
    RestoreScreenShotName ();
    return ret;
}

/**
 * D2.1
 * @return `back, `abort or `next
 */
global define symbol OutgoingDetailsDialog () ``{
    SetScreenShotName ("mail-2o-outgoing-details");

    string mod = listToString (Mail::masquerade_other_domains);
    list(string) lmod = [];
    list(map) mu = Mail::masquerade_users;

    // Translators: dialog caption
    string caption = _("Masquerading");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `RadioButtonGroup (
		`id (`mdg),
		`VBox (
		    `VSpacing (0.2),
		    WJ_MakeWidget (`from_header),
		    `VSpacing (0.2),
		    WJ_MakeWidget (`local_domains),
		    `VSpacing (0.2),
		    `Left (`RadioButton (
			       `id (`masqlocal),
			       // Translators: radio button label
			       _("Masquerade &local domains"),
			       mod == "")),
//		    `HBox (
//			`HSpacing (2),
//			`TextEntry (`id (`masqdomains), `opt (`disabled), _("That is"), ld)
////			`Left (`Label (`opt (`outputField, `hstretch), ld))
//			),
		    // Translators: radio button label
		    `Left (`RadioButton (`id (`masqothers), _("Ma&squerade other domains"), mod != "")),
		    `HBox (
			`HSpacing (2),
			// Translators: text entry label
			`TextEntry (`id (`masqdomains), `opt (`notify), _("Do&mains to masquerade"), mod)
			)
		    )
		),

	    `VSpacing (1),

	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Local user"), _("Display as")),
		makeItems (mu, ["user", "address"])
		),
/*
	    `HBox (
		`HWeight (1, `ComboBox (`id (`user), `opt (`editable), _("Local user"),
					["holly", "jane", "tarzan"])),
		`HWeight (2, `TextEntry (`id (`address), _("Display as"), "holly@red.dwarf"))
		),
*/
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("Dele&te"))
		),
	    `VSpacing (1)
	    );

    Wizard::SetContentsButtons (caption, contents, MasqueradingDialogHelp (), BackButtonLabel (), OKButtonLabel ());

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	boolean any_items = UI::QueryWidget (`id (`tab), `CurrentItem) != nil;
	UI::ChangeWidget (`id (`edit), `Enabled, any_items);
	UI::ChangeWidget (`id (`delete), `Enabled, any_items);

	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}

	if (ret == `masqdomains)
	{
	    UI::ChangeWidget (`id (`mdg), `CurrentButton, `masqothers);
	}
	else if (contains ([`add, `edit, `delete], ret))
	{
	    mu = EditTable(ret, mu, ["user", "address"], ``(MasqueradeUserPopup ()), `tab);
	}
	else if ((ret == `abort
		  && UI::ReallyAbortPopup (Mail::touched || edit_touched))
		 || ret == `back)
	{
	    break;
	}
	else if (ret == `next)
	{
	    // Input validation
	    // For consistency, all querywidgets are done here.
	    // The table contents is maintained and validated
	    // at the EditTable call.

	    symbol rb = UI::QueryWidget (`id (`mdg), `CurrentButton);
	    if (rb == `masqothers)
	    {
		mod = UI::QueryWidget (`id (`masqdomains), `Value);
		lmod = stringToList (mod);
	    }
	    else
	    {
		lmod = [];
	    }

	    if (! Validate_from_header (`from_header))
	    {
		//nothing
	    }
	    else if (! Validate_local_domains (`local_domains))
	    {
		// nothing, already done
	    }
	    else if (find (string s, lmod, ``(! check_domainname (s))) != nil)
	    {
		UI::SetFocus (`id (`masqdomains));
		// Translators: error popup
		// Already in Translation Memory
		string msg = _("The domain name is incorrect");
		// TODO: describe a valid domain name
		UI::ErrorPopup (msg);
	    }
	    else
	    {
		// all checks OK, break the input loop
		break;
	    }
	}
    }

    if (ret == `next)
    {
	// all querywidgets are already done by the validation part
	Set_from_header (`from_header);
	Set_local_domains (`local_domains);

	Mail::Touch (Mail::masquerade_other_domains != lmod);
	Mail::masquerade_other_domains = lmod;

	Mail::Touch (Mail::masquerade_users != mu);
	Mail::masquerade_users = mu;
    }
    RestoreScreenShotName ();
    return ret;
}

/**
 * (taken from y2c_users ui.ycp)
 * @param username a string
 * @return Whether a string contains only valid user name characters
 */
global define boolean check_username (string username) ``{
//DUH, auth_dialogs.ycp, users.ycp and Users.ycp have conflicing definitions!
    string valid_logname_chars  = "0123456789abcdefghijklmnopqrstuvwxyz-_";

    string firstchar = substring (username, 0, 1);
    return username != "" &&
	( (firstchar >= "a" && firstchar <= "z" ) || firstchar == "_"  ) &&
	findfirstnotof (username, valid_logname_chars) == nil;
}

/**
 * (taken from y2c_users ui.ycp)
 * @return Describe a valid username
 */
global define string valid_username () ``{
    // There is a check whether the information from the UI is
    // correct and complete.  The login name may contain only
    // certain characters and must begin with a letter.
    // Already in Translation Memory
    return _("The user login may contain only
lower case letters, digits, \"-\" and \"_\"
and must begin with a letter or \"_\".
Please try again.
");
}

/**
 * See RFC 2822, 3.4
 * But for now, nonempty, no-spaces.
 * @param address an address to check
 * @return valid?
 */
global define boolean check_mail_local_part (string address) ``{
    return address != "" && findfirstof (address, " ") == nil;
}

/**
 * See RFC 2822, 3.4
 * But for now, no-spaces@valid_domainname
 * @param address an address to check
 * @return valid?
 */
global define boolean check_mail_address (string address) ``{
    list(string) parts = splitstring (address, "@");
    if (size (parts) != 2)
    {
	return false;
    }

    return check_mail_local_part (parts[0]:"")
	&& check_domainname (parts[1]:"");
}

/**
 * A list to check entered user names against.
 * It is initialized on first use.
 */
global list(string) local_users = nil;

/**
 * Read user names from passwd.
 * It does not get the NIS entries, that's why one combo is editable.
 * "+" is filtered out.
 * @return user name list
 */
global define list(string) GetLocalUsers () ``{
    if (local_users == nil)
    {
	// initialize the list
	// CHECK .etc.passwd is in yast2
	list(map) passwd = SCR::Read (.etc.passwd);
	local_users = maplist (map entry, passwd, ``(entry["username"]:"") );
	local_users = sort (filter (string u, local_users, ``(u != "+")));
    }
    return local_users;
}

/**
 * D2.1.1
 * Used for adding and editing a user masquerading entry.
 * @param default	$["user": "address":] or just $[]
 * @param existing	current masqueading list
 * @return		$["comment": "", "user": "address":] or $[] on cancel
 */
global define map MasqueradeUserPopup (map default, list(map) existing) ``{
    string user = lookup (default, "user", "");
    string address = lookup (default, "address", "");
    list(string) forbidden = maplist (`e, existing, ``( lookup (e, "user", "") ));

    term contents =
	`HBox(
	    `HSpacing(1),
	    `VBox(
		`VSpacing(0.2),
		// Translators: popup dialog heading
		`Heading (_("Sender address rewriting")),
		Mode::config ?
		// Translators: text entry label
		`Left (`TextEntry (`id (`user), _("&Local user"), user)) :
		`Left (`ComboBox (`id (`user), `opt (`editable, `hstretch), _("&Local user"), GetLocalUsers ())),
		// Translators: text entry label
		`Left (`TextEntry (`id (`address), _("&Display as"), address)),
		`VSpacing(0.2),
		`HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
		      `PushButton(`id(`cancel), CancelButtonLabel())),
		`VSpacing(0.2)
		),
	    `HSpacing(1)
	);

    UI::OpenDialog (`opt(`decorated), contents);
    UI::ChangeWidget (`id (`user), `Value, user);
    UI::SetFocus (`id (`user));

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    break;
	}
	else if (ret == `ok)
	{
	    // Input validation
	    user = UI::QueryWidget (`id (`user), `Value);
	    address = UI::QueryWidget (`id (`address), `Value);


	    if (!check_username (user))
	    {
		UI::SetFocus (`id (`user));
		UI::ErrorPopup (valid_username ());
	    }
	    else if (contains (forbidden, user))
	    {
		UI::SetFocus (`id (`user));
		// Translators: error message
		UI::ErrorPopup (_("The address for this user is already defined."));
	    }
	    else if (!check_mail_address (address))
	    {
		// string valid_mail_address
		// no-spaces@valid_domainname
		UI::SetFocus (`id (`address));
		UI::ErrorPopup (_("The mail address format is incorrect."));
	    }
	    else
	    {
		// all checks OK, break the input loop
		break;
	    }
	}
    }

    UI::CloseDialog ();

    return (ret == `ok)? $["comment": "", "user": user, "address": address] : $[];
}

/**
 * D3
 * @return `back, `abort or `next
 */
global define symbol DownloadingDialog () ``{
    SetScreenShotName ("mail-2ia-download");
    //List of maps: $[server:, protocol:, remote_user:, local_user:, password:]
    list(map) fm = Mail::fetchmail;

    // Translators: dialog caption
    string caption = _("Mail downloading");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Server"), _("Protocol"), _("User"), _("Local user")),
		makeItems (fm, ["server", "protocol", "remote_user", "local_user"])
		),
/*
	    `HBox (
		`HWeight (1, `ComboBox (`id (`user), `opt (`editable), _("Local user"),
					["holly", "jane", "tarzan"])),
		`HWeight (2, `TextEntry (`id (`address), _("Display as"), "holly@red.dwarf"))
		),
*/
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("De&lete"))
		),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, DownloadingDialogHelp (), BackButtonLabel (), OKButtonLabel ());

    UI::ChangeWidget (`id (`edit), `Enabled, false);
    UI::ChangeWidget (`id (`delete), `Enabled, false);

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	boolean any_items = UI::QueryWidget (`id (`tab), `CurrentItem) != nil;
	UI::ChangeWidget (`id (`edit), `Enabled, any_items);
	UI::ChangeWidget (`id (`delete), `Enabled, any_items);

	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}


	if (contains ([`add, `edit, `delete], ret))
	{
	    fm = EditTable(ret, fm, ["server", "protocol", "remote_user", "local_user"], ``(FetchmailPopup ()), `tab);
	}
	else if ((ret == `abort
		  && UI::ReallyAbortPopup (Mail::touched || edit_touched))
		 || ret == `next || ret == `back)
	{
	    break;
	}
    }

    if (ret == `next)
    {
	Mail::Touch (Mail::fetchmail != fm);
	Mail::fetchmail = fm;
    }
    RestoreScreenShotName ();
    return ret;
}

/**
 * D3.1
 * @param default
 *	$[server:, protocol:, remote_user:, local_user:, password:, ...]
 * @param existing	unused
 * @return edited data (with no other fields) or $[] on cancel
 */
global define map FetchmailPopup (map default, list(map) existing) ``{
    fetchmail_item = default;
    fetchmail_item_touched = false;

    list(symbol) widgets = [
	`fm_server,
	`fm_protocol,
	`fm_remote_user,
	`fm_password,
	`fm_local_user,
	];

    term contents =
	`HBox(
	    `HSpacing(1),
	    `VBox(
		`VSpacing(0.2),
		// Translators: popup dialog heading
		`Heading (_("Mail downloading")),
		`Left (WJ_MakeWidget (`fm_server)),
		`Left (WJ_MakeWidget (`fm_protocol)),
		`Left (WJ_MakeWidget (`fm_remote_user)),
		`Left (WJ_MakeWidget (`fm_password)),
		`Left (WJ_MakeWidget (`fm_local_user)),
		`VSpacing(0.2),
		`HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
		      `PushButton(`id(`cancel), CancelButtonLabel())),
		`VSpacing(0.2)
		),
	    `HSpacing(1)
	);

    UI::OpenDialog (`opt(`decorated), contents);
    WJ_GetWidget (`fm_protocol);
    WJ_GetWidget (`fm_local_user);
    UI::SetFocus (`id (`fm_server));

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    break;
	}
	else if (ret == `ok)
	{
	    // Input validation
	    if (WJ_Validate (widgets))
	    {
		// all checks OK, break the input loop
		break;
	    }
	}
    }

    WJ_Set (widgets); // TODO hope it is ok to mess it up
    UI::CloseDialog ();

    return (ret == `ok)? fetchmail_item : $[];
}

/**
 * D1.1
 * @return `back, `abort or `next
 */
global define symbol AliasesDialog () ``{
    SetScreenShotName ("mail-2ib-aliases");
    list(map) aliases = Mail::MergeRootAlias (Mail::aliases);

    // Translators: dialog caption
    string caption = _("Aliases");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Alias"), _("Destinations")),
		makeItems (aliases, ["alias", "destinations"])
		),
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("De&lete"))
		),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, AliasesDialogHelp (), BackButtonLabel (), OKButtonLabel ());

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	boolean any_items = UI::QueryWidget (`id (`tab), `CurrentItem) != nil;
	UI::ChangeWidget (`id (`edit), `Enabled, any_items);
	UI::ChangeWidget (`id (`delete), `Enabled, any_items);

	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}

	if (contains ([`add, `edit, `delete], ret))
	{
	    aliases = EditTable(ret, aliases, ["alias", "destinations"], ``(AliasPopup ()), `tab);
	}
	else if (ret == "man_aliases")
	{
	    y2milestone ("TODO: man aliases");
	}
	else if ((ret == `abort
		  && UI::ReallyAbortPopup (Mail::touched || edit_touched))
		 || ret == `next || ret == `back)
	{
	    break;
	}
    }

    if (ret == `next)
    {
	Mail::Touch (edit_touched);
	Mail::aliases = aliases;
	Mail::FilterRootAlias ();
    }
    RestoreScreenShotName ();
    return ret;

}

/**
 * D1.1.1, 1.2.1
 * Used for adding and editing an alias/virtual domain entry.
 * @param default	$["alias": "destinations": ?comment] or just $[]
 * @param existing	current entry list
 * @return		$["comment": ""?, "alias": "destinations":] or $[] on cancel
 */
global define map AliasPopup (map default, list(map) existing) ``{
    string alias = lookup (default, "alias", "");
    string destinations = lookup (default, "destinations", "");
    list(string) forbidden = maplist (`e, existing, ``( lookup (e, "alias", "") ));

    term contents =
	`HBox(
	    `HSpacing(1),
	    `VBox(
		`VSpacing(0.2),
		// Translators: popup dialog heading
		`Heading (_("Incoming mail redirection")),
		// Translators: text entry label
		`Left (`TextEntry (`id (`alias), _("&Alias"), alias)),
		// Translators: text entry label
		`Left (`TextEntry (`id (`destinations), _("&Destinations"), destinations)),
		`VSpacing(0.2),
		`HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
		      `PushButton(`id(`cancel), CancelButtonLabel())),
		`VSpacing(0.2)
		),
	    `HSpacing(1)
	);

    UI::OpenDialog (`opt(`decorated), contents);
    UI::SetFocus (`id (`alias));

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    break;
	}
	else if (ret == `ok)
	{
	    // Input validation
	    alias = UI::QueryWidget (`id (`alias), `Value);
	    destinations = UI::QueryWidget (`id (`destinations), `Value);

	    // TODO: this only works because check_mail_local part is too
	    // permissive. Virtusertable aliases may contain @ so it is not
	    // a local part and the check will need to be improved.
	    // (But a postfix-style virtual domain will have an @-less entry.)
	    if (!check_mail_local_part (alias))
	    {
		UI::SetFocus (`id (`alias));
		// Translators: error message
		UI::MessagePopup (_("The alias format is incorrect."));
	    }
	    else if (contains (forbidden, alias))
	    {
		UI::SetFocus (`id (`alias));
		// Translators: error message
		UI::MessagePopup (_("The destinations for this alias are already defined."));
	    }
	    else
	    {
		// all checks OK, break the input loop
		break;
	    }
	}
    }

    UI::CloseDialog ();

    return (ret == `ok)? $["comment": "", "alias": alias, "destinations": destinations] : $[];
}



/**
 * D1.2
 * @return `back, `abort or `next
 */
global define symbol VirtualDialog () ``{
    SetScreenShotName ("mail-2ic-virtdomains");
    list(map) vu = Mail::virtual_users;

    // Translators: dialog caption
    string caption = _("Virtual domains");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Alias"), _("Destinations")),
		makeItems (vu, ["alias", "destinations"])
		),
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("De&lete"))
		),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, VirtualDialogHelp (), BackButtonLabel (), OKButtonLabel ());

    UI::ChangeWidget (`id (`edit), `Enabled, false);
    UI::ChangeWidget (`id (`delete), `Enabled, false);

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	boolean any_items = UI::QueryWidget (`id (`tab), `CurrentItem) != nil;
	UI::ChangeWidget (`id (`edit), `Enabled, any_items);
	UI::ChangeWidget (`id (`delete), `Enabled, any_items);

	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    ret = `abort;
	}


	if (contains ([`add, `edit, `delete], ret))
	{
	    vu = EditTable(ret, vu, ["alias", "destinations"], ``(AliasPopup ()), `tab);
	}
	else if ((ret == `abort
		  && UI::ReallyAbortPopup (Mail::touched || edit_touched))
		 || ret == `next || ret == `back)
	{
	    break;
	}
    }

    if (ret == `next)
    {
	Mail::Touch (Mail::virtual_users != vu);
	Mail::virtual_users = vu;;
    }
    RestoreScreenShotName ();
    return ret;

}

}
