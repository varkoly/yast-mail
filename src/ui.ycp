/**
 * File:
 *   include/mail/ui.ycp
 *
 * Package:
 *   Configuration of mail
 *
 * Summary:
 *   User interface functions.
 *
 * Authors:
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 *
 * All user interface functions.
 *
 */

{

textdomain "mail";

import "Wizard";
import "Wizard_hw";
import "Progress";
import "Mail";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";
include "wizard/sequencer.ycp";
include "require.ycp";

include "mail/helps.ycp";

/**
 * A replacement for the Next button: "Finish", `next
 */
global term finish_button = `PushButton (`id (`next), `opt (`default), FinishButtonLabel());

/**
 * Whole configuration of mail
 * @return `back, `abort or `next
 */
global define symbol MailSequence () ``{
    map aliases =
	$[
	    "read"	: [ ``( ReadDialog () ), true ],
	    "main"	:   ``( MainSequence () ),
	    "write"	: [ ``( WriteDialog () ), true ]
	];

    map sequence = $[
	"ws_start" : "read",
	"read" :
	$[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" :
	$[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    // Translators: dialog caption
    string caption = _("Mail configuration");
    term contents = `Label (_("Initializing ..."));

    Wizard::CreateDialog ();
    Wizard::SetContentsButtons ( caption,
				contents,
				"",
				BackButtonLabel (),
				NextButtonLabel ());

    any ret = WizardSequencer (aliases, sequence);

    UI::CloseDialog ();
    return ret;
}

/**
 * Whole configuration of mail but without reading and writing.
 * For use with autoinstallation.
 * @return `back, `abort or `next
 */
global define symbol MailAutoSequence () ``{
    // Translators: dialog caption
    string caption = _("Mail configuration");
    term contents = `Label (_("Initializing ..."));

    Wizard::CreateDialog ();
    Wizard::SetContentsButtons ( caption,
				contents,
				"",
				BackButtonLabel (),
				NextButtonLabel ());

    // Run the main configuration workflow
    any ret = MainSequence ();

    UI::CloseDialog ();
    return ret;
}

/**
 * Main workflow of the mail configuration
 * @return `back, `abort or `next
 */
global define symbol MainSequence () ``{

    map dialogs =
	$[
	    "connection_type":	``( ConnectionTypeDialog () ),
	    "outgoing":		``( OutgoingDialog () ),
	    "masquerading":	``( MasqueradingDialog () ),
	    "skip?":		[ ``( DialupOnly () ), true ],
//	    "skip?":		[ ``( (Mail::connection_type == `dialup) ? `next : `skip ), true],
	    "downloading":	``( DownloadingDialog () ),
	    "aliases":		``( AliasesDialog () ),
	    "virtual":		``( VirtualDialog () ),
	];

    map sequence = $[
	"ws_start" : "connection_type",
	"connection_type" :
	$[
	    `abort	: `abort,
	    `next	: "outgoing",
	    `none	: `next
	],
	"outgoing" :
	$[
	    `abort	: `abort,
	    `next	: "skip?",
	    `masqdetail : "masquerading",
	],
	"masquerading" :
	$[
	    `abort	: `abort,
	    `ok		: "outgoing",
	],
	"skip?" :
	$[
	    `next	: "downloading",
	    `skip	: "aliases",
	],
	"downloading" :
	$[
	    `abort	: `abort,
	    `next	: "aliases",
	],
	"aliases" :
	$[
	    `abort	: `abort,
	    `next	: "virtual",
	],
	"virtual" :
	$[
	    `abort	: `abort,
	    `next	: `next,
	],
    ];

    any ret = WizardSequencer (dialogs, sequence);

    return ret;
}



/**
 * Read settings dialog
 * @return `abort or `next
 */
global define symbol ReadDialog () ``{
    // Set help text
    Wizard::RestoreHelp (ReadDialogHelp ());

    // A callback function for abort
    block callback = ``{
	return UI::PollInput () == `abort;
    };

    // Read the configuration
    boolean was_ok = Mail::Read ( callback );

    // TODO FIXME possibly handle the abort
    if (was_ok)
    {
	if (! Mail::CreateConfig ())
	{
	    string setting = "MAIL_CREATE_CONFIG";
	    // Translators: continue/cancel dialog
	    // %1 is a sysconfig variable name
	    was_ok = UI::ContinueCancelPopup (sformat (_(
"The setting %1 is turned off so you have
probably modified the configuration files directly.
If you continue, it will be turned on and
SuSEconfig will overwrite your changes."), setting));
	}
    }

    return ( was_ok? `next : `abort );
}

/**
 * Write settings dialog
 * @return `abort or `next
 */
global define symbol WriteDialog () ``{
    // Install amavis package if it is not there.
    // Cannot do it in Write, autoinstall does it differently.
    if (Mail::use_amavis)
    {
	string pkg = (Mail::mta == `sendmail)? "amavis-sendmail":"amavis-postfix";
	if (! SCR::Read (.targetpkg.installed, pkg))
	{
	    // require.ycp
	    DoInstallAndRemove ([pkg], []);
	}
    }

    // Set help text
    Wizard::RestoreHelp (WriteDialogHelp ());

    // A callback function for abort
    block callback = ``{
	return UI::PollInput () == `abort;
    };

    // Read the configuration
    boolean was_ok = Mail::Write ( callback );

    // TODO FIXME possibly handle the abort

    return ( was_ok? `next : `abort );
}

/* ---------------------------------------------------------------- */

/**
 * @param data	a list of structs
 * @param keys	which members to put in the table
 * @return	an item list
 */
global define list(term) makeItems (list(map) data, list(string) keys) ``{
    integer i = 0;
    return maplist (`d, data, ``{
	term t = `item (`id (i));
	i = i + 1;
	foreach (`k, keys, ``{
	    t = add (t, lookup (d, k, ""));
	});
	return t;
    });
}

global boolean edit_touched = false;

/**
 * A generic handler for editing tables.
 * The current item of table_widget
 * makeItems (new_data, keys) is used to fill table_widget
 * @param action	`add, `edit or `delete
 * @param data		the data edited using the table
 * @param keys		keys of respective columns
 * @param editEntry	a function to edit an entry:
 *			gets the current entry and list of othe entries as parameters
 * @param table_widget	id of the table (usually a symbol)
 * @return		the edited data
 */
global define list(map) EditTable (symbol action,
				   list(map) data,
				   list(string) keys,
				   term editEntry,
				   any table_widget) ``{
    list(map) new_data = nil;
    integer entryno = UI::QueryWidget (`id (table_widget), `CurrentItem);
    boolean touched = false;
    if (action == `add)
    {
	editEntry = add (editEntry, $[]);
	editEntry = add (editEntry, data);
	map entry = eval (editEntry);
	if (size (entry) > 0)
	{
	    new_data = add (data, entry);
	    touched = true;
	}
	else
	{
	    new_data = data;
	}
    }
    else if (action == `edit)
    {
	editEntry = add (editEntry, select (data, entryno, $[]));
	editEntry = add (editEntry, remove (data, entryno));
	map entry = eval (editEntry);
	if (size (entry) > 0)
	{
	    integer i = 0;
	    new_data = maplist (`e, data, ``{
		i = i + 1;
		return (i - 1 == entryno)? entry: e;
	    });
	    touched = true;
	}
	else
	{
	    new_data = data;
	}
    }
    else if (action == `delete)
    {
	new_data = remove (data, entryno);
	touched = true;
    }
    else
    {
	y2error ("Unknown EditTable action %1.", action);
	new_data = data; // be nice
    }

    if (touched)
    {
	UI::ChangeWidget (`id (table_widget), `Items, makeItems (new_data, keys));
	edit_touched = true;
    }
    return new_data;
}

/* ---------------------------------------------------------------- */

/**
 * D1
 * @return `back, `abort, `next or `none
 */
global define symbol ConnectionTypeDialog () ``{
    symbol ct = Mail::connection_type;
    boolean amavis = Mail::use_amavis;

    // Translators: dialog caption
    string caption = _("Connection type");
    term contents =
	`Frame (
	    // Translators: frame label
	    _("Connection type"),
	    `RadioButtonGroup (
		`id (`ctg),
		`HSquash (
		    `VBox (
			`VSpacing (0.2),
			// Translators: radio button label
			`Left (`RadioButton (`id (`permanent), `opt (`notify), _("&Permanent"), ct == `permanent)),
			// Translators: radio button label
			`Left (`RadioButton (`id (`dialup), `opt (`notify), _("&Dial-up"), ct == `dialup)),
			// Translators: radio button label
			`Left (`RadioButton (`id (`none), `opt (`notify), _("No &connection"), ct == `none)),
/*
			`VSpacing (0.5),
			`Left (`RadioButton (`id (`dont), _("Do not configure"), false)),
*/
			`VSpacing (0.2)
			)
		    )
		)
	    );

    contents = `VBox (
	contents,
	`VSpacing (1),
	// Translators: check box label
	`CheckBox (`id (`amavis), _("Enable &virus scanning (AMaViS)"), amavis)
	);

    Wizard::SetContentsButtons (caption, contents, ConnectionTypeDialogHelp (), BackButtonLabel (), NextButtonLabel ());

    any ret = nil;
    while (true)
    {
	ct = UI::QueryWidget (`id (`ctg), `CurrentButton);
	if (ct == `permanent || ct == `dialup)
	{
	    UI::ChangeWidget (`id (`next), `Label, NextButtonLabel());
	    //argh, slow
	    //Wizard::RestoreNextButton ();
	    UI::ChangeWidget (`id (`amavis), `Enabled, true);
	}
	else if (ret == `none)
	{
	    UI::ChangeWidget (`id (`next), `Label, FinishButtonLabel());
	    //argh, slow
	    //Wizard::ReplaceNextButton (finish_button);
	    UI::ChangeWidget (`id (`amavis), `Value, false);
	    UI::ChangeWidget (`id (`amavis), `Enabled, false);
	}

	ret = UI::UserInput ();
	if (ret == `back || ret == `next ||
	    (ret == `abort && UI::ReallyAbortPopup (Mail::touched)))
	{
	    break;
	}
    }

    if (ret == `next)
    {
	ct = UI::QueryWidget (`id (`ctg), `CurrentButton);
	Mail::Touch (Mail::connection_type != ct);
	Mail::connection_type = ct;

	amavis = UI::QueryWidget (`id (`amavis), `Value);
	Mail::Touch (Mail::use_amavis != amavis);
	Mail::use_amavis = amavis;

	ret = (ct == `none)? `none: ret;
    }
    return ret;
}

/**
 * Formats a list for a TextEntry, separating the elements by ", "
 * @param alist	a list
 * @return	a string
 */
global define string listToString (list(string) alist) ``{
    return mergestring (alist, ", ");
}

/**
 * Splits a TextEntry string into a list of strings
 * separated by spaces, commas or semicolons.
 * Empty strings are removed.
 * @param astring	a string
 * @return		a list of strings
 */
global define list(string) stringToList (string astring) ``{
    list(string) alist = splitstring (astring, " ,;");
    return filter (`s, alist, ``(s != ""));
}

/**
 * D2
 * @return `back, `abort, `next or `masqdetail
 */
global define symbol OutgoingDialog () ``{
    string ld = listToString (Mail::local_domains);
    string oms = Mail::outgoing_mail_server;
    string fh = Mail::from_header;

    // Translators: dialog caption
    string caption = _("Outgoing mail");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    // Translators: text entry label
	    `TextEntry (`id (`local), _("&Domains for locally delivered mail"), ld),
	    `VSpacing (0.5),
	    // Translators: text entry label
	    `TextEntry (`id (`relay), _("&Outgoing mail server"), oms),
	    `VSpacing (0.5),
	    // Translators: text entry label
	    `TextEntry (`id (`from), _("Domain for the '&From' header"), fh),
	    // Translators: button
	    `PushButton (`id (`masqdetail), _("Detailed &address rewriting ...")),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, OutgoingDialogHelp (), BackButtonLabel (), NextButtonLabel ());

    any ret = nil;
    // The only input comes from the buttons.
    // Loop only on a canceled abort.
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `abort && ! UI::ReallyAbortPopup (Mail::touched))
	{
	    continue;
	}
	break;
    }

    if (ret == `next || ret == `masqdetail)
    {
	ld = UI::QueryWidget (`id (`local), `Value);
	list(string) lld = stringToList(ld);
	Mail::Touch (Mail::local_domains != lld);
	Mail::local_domains = lld;

	oms = UI::QueryWidget (`id (`relay), `Value);
	Mail::Touch (Mail::outgoing_mail_server != oms);
	Mail::outgoing_mail_server = oms;

	fh = UI::QueryWidget (`id (`from), `Value);
	Mail::Touch (Mail::from_header != fh);
	Mail::from_header = fh;
    }
    return ret;
}

/**
 * D2.1
 * @return `back, `abort or `ok
 */
global define symbol MasqueradingDialog () ``{

    string ld = listToString (Mail::local_domains);
    string mod = listToString (Mail::masquerade_other_domains);
    list(map) mu = Mail::masquerade_users;

    // Translators: dialog caption
    string caption = _("Detailed masquerading");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `RadioButtonGroup (
		`id (`mdg),
		`VBox (
		    `VSpacing (0.2),
		    `Left (`RadioButton (
			       `id (`masqlocal),
			       // Translators: radio button label
			       sformat (_("Masquerade &local domains (%1)"), ld),
			       mod == "")),
//		    `HBox (
//			`HSpacing (2),
//			`TextEntry (`id (`masqdomains), `opt (`disabled), _("That is"), ld)
////			`Left (`Label (`opt (`outputField, `hstretch), ld))
//			),
		    // Translators: radio button label
		    `Left (`RadioButton (`id (`masqothers), _("Ma&squerade other domains"), mod != "")),
		    `HBox (
			`HSpacing (2),
			// Translators: text entry label
			`TextEntry (`id (`masqdomains), `opt (`notify), _("Do&mains to masquerade"), mod)
			)
		    )
		),

	    `VSpacing (1),

	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Local user"), _("Present as")),
		makeItems (mu, ["user", "address"])
		),
/*
	    `HBox (
		`HWeight (1, `ComboBox (`id (`user), `opt (`editable), _("Local user"),
					["holly", "jane", "tarzan"])),
		`HWeight (2, `TextEntry (`id (`address), _("Present as"), "holly@red.dwarf"))
		),
*/
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("Dele&te"))
		),
	    `VSpacing (1)
	    );

    Wizard::SetContentsButtons (caption, contents, MasqueradingDialogHelp (), BackButtonLabel (), OKButtonLabel ());

    UI::ChangeWidget (`id (`edit), `Enabled, false);
    UI::ChangeWidget (`id (`delete), `Enabled, false);

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	ret = UI::UserInput ();

	if (ret == `masqdomains)
	{
	    UI::ChangeWidget (`id (`mdg), `CurrentButton, `masqothers);
	}
	else if (ret == `tab)
	{
	    UI::ChangeWidget (`id (`edit), `Enabled, true);
	    UI::ChangeWidget (`id (`delete), `Enabled, true);
	}
	else if (contains ([`add, `edit, `delete], ret))
	{
	    mu = EditTable(ret, mu, ["user", "address"], ``(MasqueradeUserPopup ()), `tab);
	}
	else if (ret == `abort && !UI::ReallyAbortPopup (Mail::touched || edit_touched))
	{
	    continue;
	}
	else
	{
	    break;
	}
    }

    if (ret == `next)
    {
	ret = `ok;

	list(string) lmod = [];
	symbol rb = UI::QueryWidget (`id (`mdg), `CurrentButton);
	if (rb == `masqothers)
	{
	    mod = UI::QueryWidget (`id (`masqdomains), `Value);
	    lmod = listToString (mod);
	}
	Mail::Touch (Mail::masquerade_other_domains != lmod);
	Mail::masquerade_other_domains = lmod;

	Mail::Touch (Mail::masquerade_users != mu);
	Mail::masquerade_users = mu;
    }
    return ret;
}

/**
 * D2.1.1
 * Used for adding and editing a user masquerading entry.
 * @param default	$["user": "address":] or just $[]
 * @param existing	current masqueading list
 * @return		$["comment": "", "user": "address":] or $[] on cancel
 */
global define map MasqueradeUserPopup (map default, list(map) existing) ``{
    string user = lookup (default, "user", "");
    string address = lookup (default, "address", "");
    list(string) forbidden = maplist (`e, existing, ``( lookup (e, "user", "") ));

    term contents =
	`HBox(
	    `HSpacing(1),
	    `VBox(
		`VSpacing(0.2),
		// TODO: combo
//		`Left (`ComboBox (`id (`user), /*`opt (`editable),*/ _("Local user"),
//			   ["holly", "jane", "tarzan"])),
		// Translators: text entry label
		`Left (`TextEntry (`id (`user), _("&Local user"), user)),
		// Translators: text entry label
		`Left (`TextEntry (`id (`address), _("&Present as"), address)),
		`VSpacing(0.2),
		`HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
		      `PushButton(`id(`cancel), CancelButtonLabel())),
		`VSpacing(0.2)
		),
	    `HSpacing(1)
	);

    UI::OpenDialog (`opt(`decorated), contents);

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    break;
	}
	else if (ret == `ok)
	{
	    // TODO cleanup
	    user = UI::QueryWidget (`id (`user), `Value);
	    address = UI::QueryWidget (`id (`address), `Value);
	    if (! contains (forbidden, user))
	    {
		break;
	    }
	    // Translators: error message
	    UI::MessagePopup (_("The address for this user is already defined."));
	}
    }

    UI::CloseDialog ();

    return (ret == `ok)? $["comment": "", "user": user, "address": address] : $[];
}

/**
 * @return `next if connection_type is `dialup, otherwise `skip
 */
global define symbol DialupOnly () ``{
    return Mail::connection_type == `dialup ? `next: `skip;
}

/**
 * D3
 * @return `back, `abort or `next
 */
global define symbol DownloadingDialog () ``{
    //List of maps: $[server:, protocol:, remote_user:, local_user:, password:]
    list(map) fm = Mail::fetchmail;

    // Translators: dialog caption
    string caption = _("Mail downloading");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Server"), _("Protocol"), _("User"), _("Local user")),
		makeItems (fm, ["server", "protocol", "remote_user", "local_user"])
		),
/*
	    `HBox (
		`HWeight (1, `ComboBox (`id (`user), `opt (`editable), _("Local user"),
					["holly", "jane", "tarzan"])),
		`HWeight (2, `TextEntry (`id (`address), _("Present as"), "holly@red.dwarf"))
		),
*/
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("De&lete"))
		),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, DownloadingDialogHelp (), BackButtonLabel (), NextButtonLabel ());

    UI::ChangeWidget (`id (`edit), `Enabled, false);
    UI::ChangeWidget (`id (`delete), `Enabled, false);

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	ret = UI::UserInput ();

	if (ret == `tab)
	{
	    UI::ChangeWidget (`id (`edit), `Enabled, true);
	    UI::ChangeWidget (`id (`delete), `Enabled, true);
	}
	else if (contains ([`add, `edit, `delete], ret))
	{
	    fm = EditTable(ret, fm, ["server", "protocol", "remote_user", "local_user"], ``(FetchmailPopup ()), `tab);
	}
	else if (ret == `abort && !UI::ReallyAbortPopup (Mail::touched || edit_touched))
	{
	    continue;
	}
	else
	{
	    break;
	}
    }

    if (ret == `next)
    {
	Mail::Touch (Mail::fetchmail != fm);
	Mail::fetchmail = fm;
    }
    return ret;
}

/**
 * D3.1
 * @param existing	unused
 */
global define map FetchmailPopup (map default, list(map) existing) ``{
    string server = lookup (default, "server", "");
    string protocol = lookup (default, "protocol", "AUTO");
    string ruser = lookup (default, "remote_user", "");
    string password = lookup (default, "password", "");
    string luser = lookup (default, "local_user", "");

    term contents =
	`HBox(
	    `HSpacing(1),
	    `VBox(
		`VSpacing(0.2),
		// Translators: text entry label
		`Left (`TextEntry (`id (`server), _("&Server"), server)),
		// Translators: combo box label
		`Left (`ComboBox (`id (`protocol), _("&Protocol"), Mail::protocol_choices)),
		// Translators: text entry label
		`Left (`TextEntry (`id (`ruser), _("&Remote user name"), ruser)),
		// Translators: text entry label
		`Left (`Password (`id (`password1), _("P&assword"), password)),
		// Translators: text entry label
		`Left (`Password (`id (`password2), _("Pass&word once more"), password)),
		// Translators: text entry label
		`Left (`TextEntry (`id (`luser), _("&Local user"), luser)),
//TODO combo
//		`Left (`ComboBox (`id (`luser), _("Local user"), ["jane", "holly", "tarzan"])),
		`VSpacing(0.2),
		`HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
		      `PushButton(`id(`cancel), CancelButtonLabel())),
		`VSpacing(0.2)
		),
	    `HSpacing(1)
	);

    UI::OpenDialog (`opt(`decorated), contents);
    UI::ChangeWidget (`id (`protocol), `Value, protocol);

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    break;
	}
	else if (ret == `ok)
	{
	    // TODO cleanup
	    server = UI::QueryWidget (`id (`server), `Value);
	    protocol = UI::QueryWidget (`id (`protocol), `Value);
	    ruser = UI::QueryWidget (`id (`ruser), `Value);
	    password = UI::QueryWidget (`id (`password1), `Value);
	    luser = UI::QueryWidget (`id (`luser), `Value);

	    string password2 = UI::QueryWidget (`id (`password2), `Value);
	    if (password == password2)
	    {
		break;
	    }
	    // Translators: error message
	    UI::MessagePopup (_("The passwords do not match."));
	}
    }

    UI::CloseDialog ();

    return (ret == `ok)? $["server": server, "protocol": protocol, "remote_user": ruser, "password": password, "local_user": luser] : $[];
}

/**
 * D4
 * @return `back, `abort or `next
 */
global define symbol AliasesDialog () ``{
    list(map) aliases = Mail::aliases;

    // Translators: dialog caption
    string caption = _("Aliases");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Alias"), _("Destinations")),
		makeItems (aliases, ["alias", "destinations"])
		),
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("De&lete"))
		),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, AliasesDialogHelp (), BackButtonLabel (), NextButtonLabel ());

    UI::ChangeWidget (`id (`edit), `Enabled, false);
    UI::ChangeWidget (`id (`delete), `Enabled, false);

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	ret = UI::UserInput ();

	if (ret == `tab)
	{
	    UI::ChangeWidget (`id (`edit), `Enabled, true);
	    UI::ChangeWidget (`id (`delete), `Enabled, true);
	}
	else if (contains ([`add, `edit, `delete], ret))
	{
	    aliases = EditTable(ret, aliases, ["alias", "destinations"], ``(AliasPopup ()), `tab);
	}
	else if (ret == "man_aliases")
	{
	    y2milestone ("TODO: man aliases");
	}
	else if (ret == `abort && !UI::ReallyAbortPopup (Mail::touched || edit_touched))
	{
	    continue;
	}
	else
	{
	    break;
	}
    }

    if (ret == `next)
    {
	Mail::Touch (Mail::aliases != aliases);
	Mail::aliases = aliases;
    }
    return ret;

}

/**
 * D4.1, 5.1
 * Used for adding and editing an alias/virtual domain entry.
 * @param default	$["alias": "destinations": ?comment] or just $[]
 * @param existing	current entry list
 * @return		$["comment": ""?, "alias": "destinations":] or $[] on cancel
 */
global define map AliasPopup (map default, list(map) existing) ``{
    string alias = lookup (default, "alias", "");
    string destinations = lookup (default, "destinations", "");
    list(string) forbidden = maplist (`e, existing, ``( lookup (e, "alias", "") ));

    term contents =
	`HBox(
	    `HSpacing(1),
	    `VBox(
		`VSpacing(0.2),
		// Translators: text entry label
		`Left (`TextEntry (`id (`alias), _("&Alias"), alias)),
		// Translators: text entry label
		`Left (`TextEntry (`id (`destinations), _("&Destinations"), destinations)),
		`VSpacing(0.2),
		`HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
		      `PushButton(`id(`cancel), CancelButtonLabel())),
		`VSpacing(0.2)
		),
	    `HSpacing(1)
	);

    UI::OpenDialog (`opt(`decorated), contents);

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `cancel)
	{
	    break;
	}
	else if (ret == `ok)
	{
	    // TODO cleanup
	    alias = UI::QueryWidget (`id (`alias), `Value);
	    destinations = UI::QueryWidget (`id (`destinations), `Value);
	    if (! contains (forbidden, alias))
	    {
		break;
	    }
	    // Translators: error message
	    UI::MessagePopup (_("The destinations for this alias are already defined."));
	}
    }

    UI::CloseDialog ();

    return (ret == `ok)? $["comment": "", "alias": alias, "destinations": destinations] : $[];
}



/**
 * D5
 * @return `back, `abort or `next
 */
global define symbol VirtualDialog () ``{
    list(map) vu = Mail::virtual_users;

    // Translators: dialog caption
    string caption = _("Virtual domains");
    term contents =
	`VBox (
	    `VSpacing (0.2),
	    `Table (
		`id (`tab),
		`opt (`notify, `immediate),
		// Translators: table column headings
		`header (_("Alias"), _("Destinations")),
		makeItems (vu, ["alias", "destinations"])
		),
	    `HBox(
		// Translators: button
		`PushButton(`id(`add), _("A&dd")),
		// Translators: button
		`PushButton(`id(`edit), _("&Edit")),
		// Translators: button
		`PushButton(`id(`delete), _("De&lete"))
		),
	    `VSpacing (0.2)
	    );

    Wizard::SetContentsButtons (caption, contents, VirtualDialogHelp (), BackButtonLabel (), FinishButtonLabel ());

    UI::ChangeWidget (`id (`edit), `Enabled, false);
    UI::ChangeWidget (`id (`delete), `Enabled, false);

    any ret = nil;
    edit_touched = false;
    while (true)
    {
	ret = UI::UserInput ();

	if (ret == `tab)
	{
	    UI::ChangeWidget (`id (`edit), `Enabled, true);
	    UI::ChangeWidget (`id (`delete), `Enabled, true);
	}
	else if (contains ([`add, `edit, `delete], ret))
	{
	    vu = EditTable(ret, vu, ["alias", "destinations"], ``(AliasPopup ()), `tab);
	}
	else if (ret == `abort && !UI::ReallyAbortPopup (Mail::touched || edit_touched))
	{
	    continue;
	}
	else
	{
	    break;
	}
    }

    if (ret == `next)
    {
	Mail::Touch (Mail::virtual_users != vu);
	Mail::virtual_users = vu;;
    }
    return ret;

}

}
